
group 'com.github.noconnor'
version '1.16.0'

dependencies {
    implementation "org.apache.commons:commons-lang3:3.4"
    implementation "ch.qos.logback:logback-classic:1.1.3"
    implementation "com.google.guava:guava:19.0"
    implementation "commons-collections:commons-collections:3.2.2"
    implementation 'org.apache.commons:commons-math3:3.6.1'

    // Reporting
    implementation "org.jtwig:jtwig-core:5.86.1.RELEASE"
    compileOnly "org.projectlombok:lombok:1.16.18"
    annotationProcessor "org.projectlombok:lombok:1.16.18"
}

jar {
    archivesBaseName = 'junitperf-core'
    duplicatesStrategy "exclude"
}

task javadocJar(type: Jar) {
    from javadoc.destinationDir
    archiveClassifier = 'javadoc'
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

// http://blog.sonatype.com/2010/01/how-to-generate-pgp-signatures-with-maven/
signing {
    sign configurations.archives
}

// http://central.sonatype.org/pages/gradle.html
uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: findProperty('ossrhUsername'), password: findProperty('ossrhPassword'))
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: findProperty('ossrhUsername'), password: findProperty('ossrhPassword'))
            }

            pom.project {
                name = 'junitperf-core'
                artifactId 'junitperf-core'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'API performance testing framework built using JUnit'
                url 'https://github.com/noconnor/JUnitPerf'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                scm {
                    connection 'scm:git@github.com:noconnor/JUnitPerf.git'
                    developerConnection 'scm:git@github.com:noconnor/JUnitPerf.git'
                    url 'git@github.com:noconnor/JUnitPerf.git'
                }

                developers {
                    developer {
                        id 'noconnor'
                        name = 'noconnor'
                        email 'noconnorie@protonmail.com'
                    }
                }

            }
        }
    }
}