apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'com.github.noconnor'
sourceCompatibility = 1.8
version = '1.0.0'

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile "org.projectlombok:lombok:1.16.16"
    compile "org.apache.commons:commons-lang3:3.4"
    compile "ch.qos.logback:logback-classic:1.1.3"
    compile "com.google.guava:guava:19.0"
    compile "commons-collections:commons-collections:3.2.2"
    compile 'junit:junit:4.12'
    compile 'commons-math:commons-math:1.2'
    compile 'org.mockito:mockito-core:2.11.0'
    compile "org.hamcrest:hamcrest-junit:2.0.0.0"
    // Reporting
    compile "org.jtwig:jtwig-core:5.86.1.RELEASE"
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

// http://blog.sonatype.com/2010/01/how-to-generate-pgp-signatures-with-maven/
signing {
    sign configurations.archives
}

// http://central.sonatype.org/pages/gradle.html
uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUser, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUser, password: ossrhPassword)
            }

            pom.project {
                name 'junitperf'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'API performance testing framework built using JUnit'
                url 'https://github.com/noconnor/JUnitPerf'

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                scm {
                    connection 'scm:git@github.com:noconnor/JUnitPerf.git'
                    developerConnection 'scm:git@github.com:noconnor/JUnitPerf.git'
                    url 'git@github.com:noconnor/JUnitPerf.git'
                }

                developers {
                    developer {
                        id 'noconnor'
                        name 'noconnor'
                        email 'noconnorie@protonmail.com'
                    }
                }

            }
        }
    }
}
